plugins {
    id "java"
    id "idea"
    id "jacoco"
    id "maven-publish"
    id 'signing'
    id "io.codearte.nexus-staging" version "0.21.2"  // logs into Sonatype OSS and does a "Close" and "Release". ./gradlew closeAndReleaseRepository
    id "com.github.ben-manes.versions" version "0.28.0"
}

group = "${cloudReporterGroupId}"
version = "${cloudReporterVersion}"

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    withJavadocJar()
    withSourcesJar()
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.encoding = 'UTF-8'
}


javadoc {
    if (JavaVersion.current().isJava8Compatible()) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }

    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.maven.apache.org/maven2" }
    jcenter { url "https://jcenter.bintray.com" }
}

dependencies {
    implementation("io.dropwizard.metrics:metrics-core:4.1.8")
    implementation("io.dropwizard.metrics:metrics-jvm:4.1.8")
    implementation("software.amazon.awssdk:cloudwatch:2.13.8")
    implementation("org.slf4j:slf4j-api:1.7.30")

    testImplementation("org.mockito:mockito-core:3.3.3")
    testImplementation("junit:junit:4.13")
    testImplementation("com.google.truth:truth:1.0.1")
    testImplementation("org.hamcrest:hamcrest-core:2.2")
    testImplementation("org.hamcrest:hamcrest-library:2.2")
}

tasks.withType(Test) {
    testLogging {
        events /*"passed", */ "skipped", "failed"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}


apply from: "$rootDir/conf/jacoco.gradle"
apply from: "$rootDir/conf/jar.gradle"
apply from: "$rootDir/conf/sonatype.gradle"
